import QtQuick
import QtQuick.Window
import Qt.labs.platform
import QtQuick.Controls

ApplicationWindow  {
    id: root
    width: Screen.width * 0.5
    height: Math.max(60, Screen.height * 0.08)
    visible: true
    flags: Qt.Window | Qt.FramelessWindowHint | Qt.WindowStaysOnTopHint
    color: "transparent"

    x: (Screen.width - width) / 2
    y: -height + 4

    property bool expanded: false
    property int animationDuration: 300
    property bool dragActive: false
    property bool mouseIsInWindow: false
    property int itemWidth: 100  // ÊØè‰∏™Êñá‰ª∂È°πÁöÑÂÆΩÂ∫¶
    property int itemHeight: 80  // ÊØè‰∏™Êñá‰ª∂È°πÁöÑÈ´òÂ∫¶
    property int itemsPerRow: Math.max(1, Math.floor((width - 40) / itemWidth)) // ÊØèË°åÊòæÁ§∫ÁöÑÊñá‰ª∂Êï∞Èáè

    // ËøûÊé•Áä∂ÊÄÅÂ±ûÊÄß
    property string current_device: ""
    property bool isConnected: false
    property string connectionStatus: isConnected ? current_device : "Êú™ËøûÊé•"

    property var currentAcceptHandler: null
    property var currentRejectHandler: null

    Loader {
        id: deviceWindowLoader
        source: "qrc:/qml/ui/DeviceListWindow.qml"
        
        onLoaded: {
            item.deviceModel = device_list_model
        }
    }
    
    Loader {
        id: connectRequestLoader  
        source: "qrc:/qml/ui/ConnectRequestDialog.qml"
        
        onLoaded: {
            item.connection_model = connection_manager
        }
    }

    Connections {
        target: connection_manager
        enabled: connectRequestLoader.status === Loader.Ready
        
        function onHaveConnectError(message) {
            if (generalDialogLoader.status === Loader.Ready) {
                generalDialogLoader.item.iconType = generalDialogLoader.item.error
                generalDialogLoader.item.text = message
                generalDialogLoader.item.buttons = generalDialogLoader.item.ok
                generalDialogLoader.item.show()
                generalDialogLoader.item.requestActivate()
            }
        }
        
        function onHaveRecvError(message) {
            if (generalDialogLoader.status === Loader.Ready) {
                generalDialogLoader.item.iconType = generalDialogLoader.item.error
                generalDialogLoader.item.text = message
                generalDialogLoader.item.buttons = generalDialogLoader.item.ok
                generalDialogLoader.item.show()
                generalDialogLoader.item.requestActivate()
                resetStatus()
            }
        }
        
        function onPeerClosed() {
            if (generalDialogLoader.status === Loader.Ready) {
                generalDialogLoader.item.iconType = generalDialogLoader.item.error
                generalDialogLoader.item.text = "ÂØπÊñπÊñ≠ÂºÄËøûÊé•"
                generalDialogLoader.item.buttons = generalDialogLoader.item.ok
                generalDialogLoader.item.show()
                generalDialogLoader.item.requestActivate()
                resetStatus()
            }
            resetStatus()
        }
    }

    Loader {
        id: generalDialogLoader
        source: "qrc:/qml/ui/GeneralDialog.qml"
        onLoaded: {
            item.accepted.connect(function() {
                if (currentAcceptHandler) {
                    currentAcceptHandler()
                }
                currentAcceptHandler = null
                currentRejectHandler = null
            })
            
            item.rejected.connect(function() {
                if (currentRejectHandler) {
                    currentRejectHandler()
                }
                currentAcceptHandler = null
                currentRejectHandler = null
            })
        }
    }

    // ‰∏ªÁ™óÂè£ÁöÑÊãñÊãΩÂå∫Âüü
    DropArea {
        anchors.fill: parent
        onEntered: function(drag) {
            if (drag.hasUrls) {
                drag.accept()
                dragActive = true
                if (!root.expanded) {
                    root.expanded = true
                }
            }
        }
        onExited: {
            dragActive = false
        }
        onDropped: function(drop) {
            dragActive = false
            if (drop.hasUrls && drop.urls) {
                var newFiles = []
                for (var i = 0; i < drop.urls.length; i++) {
                    var fileUrl = drop.urls[i].toString()
                    newFiles.push(fileUrl)
                }
                file_list_model.addFiles(newFiles, false);
                drop.accept()
                
                // Ê∑ªÂä†Êñá‰ª∂ÂêéÂª∂ÈïøÊî∂Áº©Êó∂Èó¥
                extendCollapseTime()
            } else {
                console.log("Ê≤°ÊúâÊ£ÄÊµãÂà∞Êñá‰ª∂URL")
            }
        }
        
    }

    // Èº†Ê†áÂå∫Âüü
    MouseArea {
        id: mouseArea
        anchors.fill: parent
        hoverEnabled: true

        onEntered: {
            collapseTimer.stop()
            if (!root.expanded) {
                root.expanded = true
            }
        }

        onExited: {            
            if (!root.mouseIsInWindow) {
                collapseTimer.start()
            }
        }
    }

    // Âª∂ËøüÊî∂Áº©
    Timer {
        id: collapseTimer
        interval: 500
        onTriggered: {
            if (root.expanded) {
                root.expanded = false
            }
            resetCollapseTime()
        }
    }

    // Ê∑ªÂä†Êñá‰ª∂ÂêéÁöÑÂª∂ÈïøÊî∂Áº©Êó∂Èó¥
    function extendCollapseTime() {
        collapseTimer.stop()
        collapseTimer.interval = 3000  // Âª∂ÈïøÂà∞3Áßí
        collapseTimer.start()
    }

    // ÈáçÁΩÆÊî∂Áº©Êó∂Èó¥Âà∞ÈªòËÆ§ÂÄº
    function resetCollapseTime() {
        collapseTimer.interval = 500
    }

    // È°∂ÈÉ®Ëß¶ÂèëÁ™óÂè£
    Window {
        id: triggerWindow
        width: root.width
        height: 6
        x: root.x
        y: 0
        visible: root.visible
        flags: Qt.FramelessWindowHint | Qt.Tool | Qt.WindowStaysOnTopHint
        color: "transparent"

        MouseArea {
            anchors.fill: parent
            hoverEnabled: true
            onEntered: {
                if (!root.expanded) {
                    root.expanded = true
                }
            }
            onExited: {
                if (!root.mouseInWindow) {
                    collapseTimer.start()
                }
            }
        }

        // ÊãñÊãΩËøõÂÖ•Êó∂Â±ïÂºÄ
        DropArea {
            anchors.fill: parent
            onEntered: function(drag){
                console.log("Ëß¶ÂèëÁ™óÂè£: ÊãñÊãΩËøõÂÖ•, hasUrls:", drag.hasUrls)
                if (!root.expanded) {
                    root.expanded = true
                }
                if (drag.hasUrls) {
                    drag.accept()
                    collapseTimer.stop()
                }
            }
            onDropped: function(drop) {
                console.log("Ëß¶ÂèëÁ™óÂè£: Êñá‰ª∂ÊãñÊîæ, hasUrls:", drop.hasUrls, "urls count:", drop.urls ? drop.urls.length : 0)
                if (drop.hasUrls && drop.urls) {
                    var newFiles = []
                    for (var i = 0; i < drop.urls.length; i++) {
                        var fileUrl = drop.urls[i].toString()
                        newFiles.push(fileUrl)
                    }
                    file_list_model.addFiles(newFiles,false);

                    // ‰ΩøÁî®‰∏ªÁ™óÂè£ÁöÑÊ∑ªÂä†ÂáΩÊï∞Â§ÑÁêÜÈáçÂ§çÊñá‰ª∂
                    // addFilesToList(newFiles)
                    drop.accept()
                    
                    // Ê∑ªÂä†Êñá‰ª∂ÂêéÂª∂ÈïøÊî∂Áº©Êó∂Èó¥
                    extendCollapseTime()
                } else {
                    console.log("Ëß¶ÂèëÁ™óÂè£: Ê≤°ÊúâÊ£ÄÊµãÂà∞Êñá‰ª∂URL")
                }
            }
            
        }
    }

    // Âä®Áîª
    Behavior on y {
        NumberAnimation {
            duration: root.animationDuration
            easing.type: Easing.OutCubic
        }
    }

    onExpandedChanged: {
        if (expanded) {
            root.y = 0
            updateWindowHeight()
            // Â±ïÂºÄÊó∂ÊªöÂä®Âà∞Â∫ïÈÉ®
            scrollToBottom()
        } else {
            root.y = -root.height + 4
        }
    }

    // Êõ¥Êñ∞Á™óÂè£È´òÂ∫¶
    function updateWindowHeight() {
        if (root.expanded) {
            if (file_list_model.getFileCount() > 0) {
                var rowsNeeded = Math.ceil(file_list_model.getFileCount() / itemsPerRow)
                var visibleRows = Math.min(rowsNeeded, 1.5) // ÊòæÁ§∫‰∏ÄË°åÂçä
                root.height = 40 + (visibleRows * itemHeight) + 20
            } else {
                root.height = Math.max(60, Screen.height * 0.08)
            }
        }
    }

    // ÊªöÂä®Âà∞Â∫ïÈÉ®
    function scrollToBottom() {
        if (fileGridView.count > 0) {
            fileGridView.positionViewAtEnd()
            // ‰ΩøÁî®TimerÁ°Æ‰øùÂú®Â∏ÉÂ±ÄÂÆåÊàêÂêéÊªöÂä®
            scrollTimer.restart()
        }
    }

    Timer {
        id: scrollTimer
        interval: 50
        onTriggered: {
            fileGridView.positionViewAtEnd()
        }
    }

    onWidthChanged: {
        itemsPerRow = Math.max(1, Math.floor((width - 40) / itemWidth))
        updateWindowHeight()
    }

    // ËÉåÊôØ
    Rectangle {
        anchors.fill: parent
        radius: 20
        color: dragActive ? "#88A8DFF7" : "#CCF0F0F0"
        border.color: "#40000000"
        border.width: 1
    }

    // Êñá‰ª∂ÁΩëÊ†ºËßÜÂõæ
    GridView {
        id: fileGridView
        anchors {
            top: parent.top
            topMargin: 40
            left: parent.left
            leftMargin: 20
            right: parent.right
            rightMargin: 20
            bottom: parent.bottom
            bottomMargin: 10
        }
        clip: true
        model: file_list_model
        cellWidth: itemWidth
        cellHeight: itemHeight
        
        onCountChanged: {
            updateWindowHeight()
            scrollToBottom()
        }

        delegate: Rectangle {
            width: itemWidth - 5
            height: itemHeight - 5
            color: index % 2 === 0 ? "#E8F4FD" : "#FFFFFF"
            radius: 8
            border.color: "#40000000"
            border.width: 1

            // Âè≥ÈîÆËèúÂçï
            Menu {
                id: contextMenu
                MenuItem {
                    text: "ÊâìÂºÄÊñá‰ª∂"
                    enabled: model.fileStatus === file_list_model.StatusCompleted | !model.isRemote
                    onTriggered: {
                        if (model.fileUrl) {
                            Qt.openUrlExternally(model.fileUrl)
                        } else if (model.filePath) {
                            var fileUrl = model.filePath.startsWith("file://") ? model.filePath : "file:///" + model.filePath
                            Qt.openUrlExternally(fileUrl)
                        }
                    }
                }
                
                MenuSeparator {}
                
                MenuItem {
                    text: "Â§çÂà∂Êñá‰ª∂Âêç"
                    onTriggered: {
                        file_list_model.copyText(model.fileName)
                    }
                }
                
                MenuItem {
                    text: "Â§çÂà∂Êñá‰ª∂Ë∑ØÂæÑ"
                    enabled: model.filePath
                    onTriggered: {
                        if (model.filePath) {
                            file_list_model.copyText(model.filePath)
                        }
                    }
                }
                
                MenuSeparator {}
                
                MenuItem {
                    text: "‰∏ãËΩΩÊñá‰ª∂"
                    enabled: model.isRemote && model.fileStatus !== file_list_model.StatusDownloading && model.fileStatus !==file_list_model.StatusCompleted
                    onTriggered: {
                        // Ëß¶ÂèëÊñá‰ª∂‰∏ãËΩΩ
                        // file_list_model.downloadFile(index)
                    }
                }
                
                MenuItem {
                    text: "Âà∑Êñ∞"
                    onTriggered: {
                        // ÈáçÊñ∞‰∏ä‰º†Êñá‰ª∂
                        // file_list_model.retryUpload(index)
                    }
                }
                
                MenuSeparator {}
                
                MenuItem {
                    text: "Âà†Èô§"
                    onTriggered: {
                        file_list_model.removeFile(index)
                    }
                }
                
                MenuItem {
                    text: "Â±ûÊÄß"
                    onTriggered: {
                        // showFileProperties(index)
                    }
                }
            }
            
            // Êñá‰ª∂È°πÁöÑÊãñÊãΩÊ∫ê
            Drag.active: fileDragArea.drag.active
            Drag.dragType: Drag.Automatic
            Drag.supportedActions: Qt.CopyAction
            Drag.mimeData: {
                "text/uri-list": [model.fileUrl],
                "text/plain": model.filePath
            }
            
            ToolTip {
                id: fileToolTip
                visible: fileDragArea.containsMouse
                text: model.toolTip
                delay: 1500
                timeout: 5000
            }

            Column {
                anchors.centerIn: parent
                width: parent.width - 12
                spacing: 1
                
                Image {
                    width: 44
                    height: 44
                    source: model.fileIcon
                    fillMode: Image.PreserveAspectFit
                    anchors.horizontalCenter: parent.horizontalCenter
                }
                
                Text {
                    text: model.fileName
                    font.pixelSize: 11
                    color: "#2c3e50"
                    width: parent.width
                    horizontalAlignment: Text.AlignHCenter
                    wrapMode: Text.NoWrap
                    maximumLineCount: 1
                    elide: Text.ElideMiddle
                    anchors.horizontalCenter: parent.horizontalCenter
                }
                
                // Áä∂ÊÄÅË°å
                Row {
                    width: parent.width
                    spacing: 2
                    anchors.horizontalCenter: parent.horizontalCenter
                    height: 12
                    
                    // Áä∂ÊÄÅÊåáÁ§∫Âô®
                    Rectangle {
                        id: statusIndicator
                        width: 8
                        height: 8
                        radius: 4
                        visible: model.isRemote || (model.fileStatus === file_list_model.StatusUploading)
                        anchors.verticalCenter: parent.verticalCenter
                        
                        color: {
                            if (model.fileStatus === file_list_model.StatusCompleted) return "#4CAF50"    // ÂÆåÊàê - ÁªøËâ≤ 
                            else if (model.fileStatus === file_list_model.StatusDefault) return "#9E9E9E" // ÈªòËÆ§ - ÁÅ∞Ëâ≤
                            else if (model.fileStatus === file_list_model.StatusUploading) return "#2196F3" // ‰∏ä‰º†‰∏≠ - ËìùËâ≤
                            else if (model.fileStatus === file_list_model.StatusDownloading) return "#FF9800" // ‰∏ãËΩΩ‰∏≠ - Ê©ôËâ≤
                            else if (model.fileStatus === file_list_model.StatusError) return "#F44336"    // ÈîôËØØ - Á∫¢Ëâ≤ 
                            else if (model.fileStatus === file_list_model.StatusPending) return "#FFC107"  // Á≠âÂæÖ‰∏≠ - ÈªÑËâ≤
                            else return "#607D8B"  // ÂÖ∂‰ªñÁä∂ÊÄÅ - ËìùÁÅ∞Ëâ≤
                        }
                    }
                    
                    // ËøõÂ∫¶Êù° - Âè™Âú®‰º†ËæìÁä∂ÊÄÅÊòæÁ§∫
                    Rectangle {
                        id: progressBar
                        width: parent.width - statusIndicator.width - parent.spacing - 18
                        height: 3
                        radius: 1.5
                        color: "#e6e6e6"
                        anchors.verticalCenter: parent.verticalCenter
                        visible: model.fileStatus === file_list_model.StatusUploading || 
                                model.fileStatus === file_list_model.StatusDownloading
                        
                        Rectangle {
                            width: parent.width * (model.fileProgress / 100)
                            height: parent.height
                            radius: 1.5
                            color: statusIndicator.color
                        }
                    }
                    
                    // ËøõÂ∫¶ÁôæÂàÜÊØî - Âè™Âú®‰º†ËæìÁä∂ÊÄÅÊòæÁ§∫
                    Text {
                        text: qsTr("%1%").arg(model.fileProgress)
                        font.pixelSize: 7
                        color: "#666"
                        anchors.verticalCenter: parent.verticalCenter
                        visible: model.fileStatus === file_list_model.StatusUploading || 
                                model.fileStatus === file_list_model.StatusDownloading
                    }
                }
            }
            
            Item {
                id: dragProxy
                width: 1; height: 1 
                visible: false

                Drag.active: false
                Drag.dragType: Drag.Automatic
                Drag.supportedActions: Qt.CopyAction
                Drag.mimeData: {
                    "text/uri-list": model.fileUrl ? [model.fileUrl.toString()] : [],
                    "text/plain": model.filePath || ""
                }
                Drag.imageSource: model.fileIcon
            }
            // Êñá‰ª∂ÊãñÊãΩÂå∫Âüü
            MouseArea {
                id: fileDragArea
                anchors.fill: parent
                enabled: true
                hoverEnabled: true
                acceptedButtons: Qt.LeftButton | Qt.RightButton

                drag.target: dragProxy
                drag.axis: Drag.XandYAxis

                onPressed: function(mouse) {
                    if (mouse.button === Qt.RightButton) {
                        contextMenu.open()
                    } else if (mouse.button === Qt.LeftButton) {
                        if (model.fileStatus === file_list_model.StatusCompleted || !model.isRemote) {
                            dragProxy.Drag.active = true
                        }
                    }
                }

                onPositionChanged: function(mouse) {
                    if (dragProxy.visible && dragProxy.Drag.active) {
                        var globalPos = mapToGlobal(mouse.x, mouse.y)
                        var itemPos = root.contentItem.mapFromGlobal(globalPos.x, globalPos.y)
                        dragProxy.x = itemPos.x - dragProxy.width / 2
                        dragProxy.y = itemPos.y - dragProxy.height / 2
                    }
                }
                
                onReleased: {
                    dragProxy.visible = false
                    dragProxy.Drag.active = false
                }
                
                // ÂèåÂáªÊâìÂºÄÊñá‰ª∂
                onDoubleClicked: {
                    if (model.fileUrl) {
                        Qt.openUrlExternally(model.fileUrl)
                    } else if (model.filePath) {
                        var fileUrl = model.filePath.startsWith("file://") ? model.filePath : "file:///" + model.filePath
                        Qt.openUrlExternally(fileUrl)
                    }
                }
                onEntered:{
                    mouseIsInWindow = true
                }
                onExited:{
                    mouseIsInWindow = false
                }
            }
            
            // Âà†Èô§ÊåâÈíÆ
            Rectangle {
                id: deleteButton
                width: 20
                height: 20
                radius: 10
                color: deleteMouseArea.containsMouse ? "#ff6b6b" : "transparent"
                border.color: "#40000000"
                border.width: 1
                anchors {
                    top: parent.top
                    topMargin: 5
                    right: parent.right
                    rightMargin: 5
                }
                
                Text {
                    text: "√ó"
                    font.pixelSize: 12
                    font.bold: true
                    color: deleteMouseArea.containsMouse ? "white" : "#666666"
                    anchors.centerIn: parent
                }
                
                MouseArea {
                    id: deleteMouseArea
                    anchors.fill: parent
                    hoverEnabled: true
                    
                    onClicked: {
                        // ‰ªéÂàóË°®‰∏≠ÁßªÈô§Êñá‰ª∂
                        file_list_model.removeFile(index)
                    }
                }
            }
        }
        
        // Á©∫ÂàóË°®ÊèêÁ§∫
        Text {
            anchors.centerIn: parent
            text: "üìÅ ÊöÇÊó†Êñá‰ª∂ÔºåÊãñÊîæÊñá‰ª∂Âà∞Ê≠§Â§Ñ"
            font.pixelSize: 14
            color: "#7f8c8d"
            visible: fileGridView.count === 0
        }
    }

    // Ê†áÈ¢òÊ†è
    Rectangle {
        id: titleBar
        width: parent.width
        height: 40
        color: "transparent"
        
        Text {
            id: titleText
            text: dragActive ? "üîÑ ÈáäÊîæÊñá‰ª∂‰ª•Â§ÑÁêÜ" : "üîÑ Êñá‰ª∂‰∏≠ËΩ¨Á´ô"
            font.pixelSize: 14
            font.bold: true
            color: "#2c3e50"
            anchors {
                left: parent.left
                leftMargin: 20
                verticalCenter: parent.verticalCenter
            }
        }
       // ËøûÊé•Áä∂ÊÄÅÂíåÂàáÊç¢ÊåâÈíÆÂÆπÂô®
        Row {
            id: connectionContainer
            spacing: 8
            anchors {
                left: titleText.right
                leftMargin: 15
                verticalCenter: parent.verticalCenter
            }
            
            // ËøûÊé•Áä∂ÊÄÅÊñáÊú¨
            Text {
                id: connectionStatusText
                text: root.connectionStatus
                font.pixelSize: 12
                color: root.isConnected ? "#27ae60" : "#e74c3c"
                anchors.verticalCenter: parent.verticalCenter
            }
            
            // ÂàáÊç¢ÊåâÈíÆ
            Rectangle {
                id: switchButton
                width: 50
                height: 24
                radius: 12
                color: switchMouseArea.containsMouse ? (root.isConnected ? "#e74c3c" : "#27ae60") : "#CCCCCC"
                border.color: "#40000000"
                border.width: 1
                anchors.verticalCenter: parent.verticalCenter
                
                Text {
                    text: isConnected ? "Êñ≠ÂºÄËøûÊé•" : "ËøûÊé•"
                    font.pixelSize: 10
                    color: "white"
                    font.bold: true
                    anchors.centerIn: parent
                }
                
                MouseArea {
                    id: switchMouseArea
                    anchors.fill: parent
                    hoverEnabled: true
                    onClicked: {
                        //Â∞öÊú™Âª∫Á´ãËøûÊé•ÔºåÂàôÊòØÊâìÂºÄËÆæÂ§áÊü•Êâæ
                        if(!isConnected){
                            if (deviceWindowLoader.status === Loader.Ready) {
                                    deviceWindowLoader.item.show()
                                    deviceWindowLoader.item.requestActivate()
                                } else {
                                    console.error("ËÆæÂ§áÁ™óÂè£Êú™Ê≠£Á°ÆÂä†ËΩΩ:", deviceWindowLoader.status)
                                }
                        }else{//Â∑≤Âª∫Á´ãËøûÊé•ÔºåÂàôÊòØÊñ≠ÂºÄËøûÊé•
                            if (generalDialogLoader.status === Loader.Ready) {
                                generalDialogLoader.item.iconType = generalDialogLoader.item.info
                                generalDialogLoader.item.text = "Á°ÆÂÆöÊñ≠ÂºÄËøûÊé•Ôºü"
                                generalDialogLoader.item.buttons = generalDialogLoader.item.yes | generalDialogLoader.item.no
                                
                                // Âä®ÊÄÅËÆæÁΩÆÂΩìÂâçÁöÑÂ§ÑÁêÜÂáΩÊï∞
                                root.currentAcceptHandler = function() {
                                    resetStatus()
                                    connection_manager.disconnect()
                                }
                                generalDialogLoader.item.show()
                                generalDialogLoader.item.requestActivate()
                            }
                        }
                    }
                    onEntered: {
                        mouseIsInWindow = true
                    }
                    onExited: {
                        mouseIsInWindow = false
                    }
                }
            }
            Connections {
                target: connection_manager
                function onHaveConRequest(device_ip, device_name) {
                if (connectRequestLoader.status === Loader.Ready) {
                    connectRequestLoader.item.device_ip = device_ip
                    connectRequestLoader.item.device_name = device_name
                    connectRequestLoader.item.show()
                    connectRequestLoader.item.requestActivate()
                } else {
                    console.error("ËøûÊé•ËØ∑Ê±ÇÂØπËØùÊ°ÜÊú™Ê≠£Á°ÆÂä†ËΩΩ:", connectRequestLoader.status)
                }
                    }
            }
            Connections {
                target: connectRequestLoader.item
                enabled: connectRequestLoader.status === Loader.Ready
                
                function onAccepted(ip, name) {
                    current_device = (name == "UnKnown" ? ip : name);
                    isConnected = true;
                    if(fileGridView.count){
                        if (generalDialogLoader.status === Loader.Ready) {
                            generalDialogLoader.item.iconType = generalDialogLoader.item.info
                            generalDialogLoader.item.text = "ÊòØÂê¶ÂêåÊ≠•ÂΩìÂâçÊñá‰ª∂"
                            generalDialogLoader.item.buttons = generalDialogLoader.item.yes | generalDialogLoader.item.no
                                
                            // Âä®ÊÄÅËÆæÁΩÆÂΩìÂâçÁöÑÂ§ÑÁêÜÂáΩÊï∞
                            root.currentAcceptHandler = function() {
                                file_list_model.syncCurrentFiles()
                            }
                            generalDialogLoader.item.show()
                            generalDialogLoader.item.requestActivate()
                        }
                    }
                }
                
                function onRejected(ip, name) {
                }
            }
            Connections {
                target: device_list_model
                enabled: deviceWindowLoader.status === Loader.Ready
                
                function onConnectResult(ret, ip) {
                    if(ret){
                        current_device = ip;
                        isConnected = true;
                        if(fileGridView.count){
                            if (generalDialogLoader.status === Loader.Ready) {
                                generalDialogLoader.item.iconType = generalDialogLoader.item.info
                                generalDialogLoader.item.text = "ÊòØÂê¶ÂêåÊ≠•ÂΩìÂâçÊñá‰ª∂"
                                generalDialogLoader.item.buttons = generalDialogLoader.item.yes | generalDialogLoader.item.no
                                
                                // Âä®ÊÄÅËÆæÁΩÆÂΩìÂâçÁöÑÂ§ÑÁêÜÂáΩÊï∞
                                root.currentAcceptHandler = function() {
                                    file_list_model.syncCurrentFiles()
                                }
                                generalDialogLoader.item.show()
                                generalDialogLoader.item.requestActivate()
                            }
                        }
                    }
                }
            }
        }        
        // Ê∏ÖÁ©∫ÊåâÈíÆ
        Rectangle {
            id: clearButton
            width: 60
            height: 24
            radius: 12
            color: clearMouseArea.containsMouse ? "#e74c3c" : "transparent"
            border.color: "#40000000"
            border.width: 1
            visible: root.expanded && fileGridView.count > 0
            anchors {
                right: closeButton.left
                rightMargin: 10
                verticalCenter: parent.verticalCenter
            }
            
            Text {
                text: "Ê∏ÖÁ©∫"
                font.pixelSize: 12
                color: clearMouseArea.containsMouse ? "white" : "#666666"
                anchors.centerIn: parent
            }
            
            MouseArea {
                id: clearMouseArea
                anchors.fill: parent
                hoverEnabled: true
                onClicked: {
                    file_list_model.clearAll()
                }
                onEntered:{
                    mouseIsInWindow = true
                }
                onExited:{
                    mouseIsInWindow = false
                }
            }
        }
        
        // ÂÖ≥Èó≠ÊåâÈíÆ
        Rectangle {
            id: closeButton
            width: 24
            height: 24
            radius: 12
            color: closeMouseArea.containsMouse ? "#E81123" : "transparent"
            border.color: "#40000000"
            border.width: 1
            visible: root.expanded
            anchors {
                top: parent.top
                topMargin: 8
                right: parent.right
                rightMargin: 8
                verticalCenter: parent.verticalCenter
            }

            Text {
                text: "√ó"
                font.pixelSize: 16
                font.bold: true
                color: closeMouseArea.containsMouse ? "white" : "#666666"
                anchors.centerIn: parent
            }

            MouseArea {
                id: closeMouseArea
                anchors.fill: parent
                hoverEnabled: true
                onClicked: Qt.quit()
                onEntered:{
                    mouseIsInWindow = true
                }
                onExited:{
                    mouseIsInWindow = false
                }
            }
        }
    }

    // Êî∂Áº©Êó∂È°∂ÈÉ®Êù°
    Rectangle {
        width: parent.width
        height: 4
        color: "#CCF0F0F0"
        border.color: "#40000000"
        border.width: 1
        y: parent.height - 4
        visible: !root.expanded
    }
    Component.onDestruction: {

    }

    function resetStatus() {
        isConnected = false
        current_device = ""
    }
}